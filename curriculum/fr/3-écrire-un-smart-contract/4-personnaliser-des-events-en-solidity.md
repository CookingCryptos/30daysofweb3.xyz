---
title: Events personnalis√©s en Solidity
description: Define Solidity events to listen for certain actions on the blockchain.
optional: false
tweet: "I just wrote a production grade smart contract üéâ #30DaysofWeb3 @womenbuildweb3 ‚úçÔ∏è"
---

## D√©finir les √©v√©nements

Rappelons que les events en Solidity sont un moyen pour notre subgraph d'_√©couter_ des actions sp√©cifiques pour nous permettre d'effectuer des requ√™tes sur les donn√©es de notre smart contract. Nous avons des fonctions √©crites pour cr√©er un nouvel √©v√©nement sur notre plateforme, r√©pondre √† un √©v√©nement, confirmer les participants individuels, confirmer le groupe de participants, et renvoyer les fonds non r√©clam√©s au propri√©taire de l'√©v√©nement. Pour que notre subgraph puisse acc√©der aux informations de ces fonctions, nous devons les exposer via des events. Nous allons √©crire les events suivants et les _√©mettre_ √† l'int√©rieur de leur fonction correspondante :

- **NewEventCreated** : expose des donn√©es sur le nouvel √©v√©nement comme le propri√©taire, la capacit√© maximale, le propri√©taire de l'√©v√©nement, le montant du d√©p√¥t, etc.
- **NewRSVP** : expose des donn√©es sur l'utilisateur qui a RSVP et l'√©v√©nement pour lequel il a RSVP.
- **ConfirmedAttendee** : expose les donn√©es sur l'utilisateur qui a √©t√© confirm√© et l'√©v√©nement pour lequel il a √©t√© confirm√©.
- **DepositsPaid** : expose les donn√©es sur les d√©p√¥ts non r√©clam√©s envoy√©s √† l'organisateur de l'√©v√©nement.

Tous nos events sont d√©sign√©s par le mot-cl√© _event_, suivi du nom personnalis√© de notre event.

D√©finissez vos events au tout d√©but de votre fichier, entre les accolades (juste apr√®s `contract Web3RSVP {`):

```solidity
event NewEventCreated(
    bytes32 eventID,
    address creatorAddress,
    uint256 eventTimestamp,
    uint256 maxCapacity,
    uint256 deposit,
    string eventDataCID
);

event NewRSVP(bytes32 eventID, address attendeeAddress);

event ConfirmedAttendee(bytes32 eventID, address attendeeAddress);

event DepositsPaidOut(bytes32 eventID);
```

## √âmettre des Events

Maintenant que nous les avons d√©finis, nous devons utiliser _emit_ quelque part. D√©finir les events, c'est les ajouter comme un outil √† votre ceinture d'outils, mais les √©mettre, c'est sortir cet outil et l'utiliser. Chaque event doit √™tre √™tre utilis√© avec un emit l√† o√π il a du sens, apr√®s qu'une action sp√©cifique ait √©t√© effectu√©e.

Pour notre premier event, `newEventCreated`, nous devons utiliser emit √† la toute fin de notre fonction `createNewEvent`.

Pour √©mettre un √©v√©nement, nous utilisons le mot-cl√© _emit_ et nous passons ensuite les arguments, c'est-√†-dire les valeurs r√©elles que nous voulons, en fonction des param√®tres que nous avons d√©finis.

Emettez NewEventCreated √† la fin de votre fonction `createNewEvent` comme ceci :

```solidity
emit NewEventCreated(
    eventId,
    msg.sender,
    eventTimestamp,
    maxCapacity,
    deposit,
    eventDataCID
);
```

`NewRSVP` doit √™tre √©mis √† la toute fin de notre fonction `createNewRSVP` comme ceci : 

```solidity
emit NewRSVP(eventId, msg.sender);

```

`ConfirmedAttendees` doit √™tre √©mis √† la toute fin de notre fonction `confirmAttendee` comme ceci :

```solidity
emit ConfirmedAttendee(eventId, attendee);

```

`DepositPaidOut` doit √™tre √©mis √† la toute fin de notre fonctio `withdrawUnclaimedDeposits` comme ceci :

```solidity
emit DepositsPaidOut(eventId);

```

## Vous l'avez fait !

Vous avez √©crit vos fonctions, d√©fini vos structures, et d√©fini des events personnalis√©s ! Voici √† quoi devrait ressembler votre code final :

```solidity
// SPDX-License-Identifier: MIT

pragma solidity ^0.8.4;

contract Web3RSVP {

    event NewEventCreated(
        bytes32 eventID,
        address creatorAddress,
        uint256 eventTimestamp,
        uint256 maxCapacity,
        uint256 deposit,
        string eventDataCID
    );

    event NewRSVP(bytes32 eventID, address attendeeAddress);

    event ConfirmedAttendee(bytes32 eventID, address attendeeAddress);

    event DepositsPaidOut(bytes32 eventID);

    struct CreateEvent {
        bytes32 eventId;
        string eventDataCID;
        address eventOwner;
        uint256 eventTimestamp;
        uint256 deposit;
        uint256 maxCapacity;
        address[] confirmedRSVPs;
        address[] claimedRSVPs;
        bool paidOut;
    }

    mapping(bytes32 => CreateEvent) public idToEvent;

    function createNewEvent(
        uint256 eventTimestamp,
        uint256 deposit,
        uint256 maxCapacity,
        string calldata eventDataCID
    ) external {
        // g√©n√©re un eventID unique en combinant des elements et en g√©n√©rant un hash
        bytes32 eventId = keccak256(
            abi.encodePacked(
                msg.sender,
                address(this),
                eventTimestamp,
                deposit,
                maxCapacity
            )
        );

        address[] memory confirmedRSVPs;
        address[] memory claimedRSVPs;


        //cela cr√©e un nouveau struct CreateEvent et l'ajoute au mapping idToEvent
        idToEvent[eventId] = CreateEvent(
            eventId,
            eventDataCID,
            msg.sender,
            eventTimestamp,
            deposit,
            maxCapacity,
            confirmedRSVPs,
            claimedRSVPs,
            false
        );

        emit NewEventCreated(
            eventId,
            msg.sender,
            eventTimestamp,
            maxCapacity,
            deposit,
            eventDataCID
        );
    }

    function createNewRSVP(bytes32 eventId) external payable {
        // recherche d'un √©v√©nement
        CreateEvent storage myEvent = idToEvent[eventId];

        // transf√©re le d√©p√¥t sur notre contrat / exiger qu'ils envoient suffisamment d'ETH
        require(msg.value == myEvent.deposit, "NOT ENOUGH");

        // exige que l'√©v√©nement n'ait pas d√©j√† eu lieu (<eventTimestamp)
        require(block.timestamp <= myEvent.eventTimestamp, "ALREADY HAPPENED");

        // s'assurer que l'√©v√©nement est sous la capacit√© maximale
        require(
            myEvent.confirmedRSVPs.length < myEvent.maxCapacity,
            "This event has reached capacity"
        );

        // exige que msg.sender ne soit pas d√©j√† dans myEvent.confirmedRSVPs
        for (uint8 i = 0; i < myEvent.confirmedRSVPs.length; i++) {
            require(myEvent.confirmedRSVPs[i] != msg.sender, "ALREADY CONFIRMED");
        }

        myEvent.confirmedRSVPs.push(payable(msg.sender));

        emit NewRSVP(eventId, msg.sender);
    }

    function confirmAllAttendees(bytes32 eventId) external {
        // recherche d'un √©v√©nement
        CreateEvent memory myEvent = idToEvent[eventId];

        // exige que msg.sender soit le propri√©taire de l'√©v√©nement
        require(msg.sender == myEvent.eventOwner, "NOT AUTHORIZED");

        // confirme chaque participant
        for (uint8 i = 0; i < myEvent.confirmedRSVPs.length; i++) {
            confirmAttendee(eventId, myEvent.confirmedRSVPs[i]);
        }
    }

    function confirmAttendee(bytes32 eventId, address attendee) public {
        // recherche de l'√©v√©nement
        CreateEvent storage myEvent = idToEvent[eventId];

        // exige que msg.sender soit le propri√©taire de l'√©v√©nement
        require(msg.sender == myEvent.eventOwner, "NOT AUTHORIZED");

        // exigez que l'invit√© figure dans myEvent.confirmedRSVPs
        address rsvpConfirm;

        for (uint8 i = 0; i < myEvent.confirmedRSVPs.length; i++) {
            if(myEvent.confirmedRSVPs[i] == attendee){
                rsvpConfirm = myEvent.confirmedRSVPs[i];
            }
        }

        require(rsvpConfirm == attendee, "NO RSVP TO CONFIRM");


        // exige que l'attendee ne figure PAS dans la liste des claimedRSVP
        for (uint8 i = 0; i < myEvent.claimedRSVPs.length; i++) {
            require(myEvent.claimedRSVPs[i] != attendee, "ALREADY CLAIMED");
        }

        // exige que les d√©p√¥ts ne soient pas d√©j√† r√©clam√©s
        require(myEvent.paidOut == false, "ALREADY PAID OUT");

        // les ajouter √† la liste des claimedRSVP
        myEvent.claimedRSVPs.push(attendee);

        // renvoyer eth au staker https://solidity-by-example.org/sending-ether
        (bool sent,) = attendee.call{value: myEvent.deposit}("");

        // si cela √©choue
        if (!sent) {
            myEvent.claimedRSVPs.pop();
        }

        require(sent, "Failed to send Ether");

        emit ConfirmedAttendee(eventId, attendee);
    }

    function withdrawUnclaimedDeposits(bytes32 eventId) external {
        // recherche de l'√©v√©nement
        CreateEvent memory myEvent = idToEvent[eventId];

        // v√©rifie si vous avez d√©j√† pay√©
        require(!myEvent.paidOut, "ALREADY PAID");

        // v√©rifie si cela fait 7 jours depuis myEvent.eventTimestamp
        require(
            block.timestamp >= (myEvent.eventTimestamp + 7 days),
            "TOO EARLY"
        );

        // seul le propri√©taire de l'√©v√©nement peut se retirer
        require(msg.sender == myEvent.eventOwner, "MUST BE EVENT OWNER");

        // calculer combien de personnes n'ont pas r√©clam√© en comparant
        uint256 unclaimed = myEvent.confirmedRSVPs.length - myEvent.claimedRSVPs.length;

        uint256 payout = unclaimed * myEvent.deposit;

        // marquez comme pay√© avant l'envoi pour √©viter une reentrancy attack
        myEvent.paidOut = true;

        // envoie le paiement au propri√©taire
        (bool sent, ) = msg.sender.call{value: payout}("");

        // si cela √©choue
        if (!sent) {
            myEvent.paidOut = false;
        }

        require(sent, "Failed to send Ether");

        emit DepositsPaidOut(eventId);
    }
}
```

## ‚úã Need Help?

If you need help, check to see if your question has already been asked in **#section-3-help**. If you don't see it in there, post a question with any details that would make it easy for a team member to help you. We'll answer most frequently asked questions in live office hours, so keep an eye out in **#announcements** for those!

## üèù Temps de pause

Vous venez d'√©crire des √©v√©nements personnalis√©s et de d√©finir toutes les fonctions n√©cessaires au fonctionnement de Web3RSVP ! Appuyez sur le bouton Partager ci-dessous pour tweeter et participer √† la chance de gagner l'un de nos prix üëÄ. Collez votre tweet dans **#builders-hype**, et nous choisirons un gagnant dans les prochaines 72 heures.

**Fin du 4√®me jour**

---

√âcrivains: [Cami](https://twitter.com/camiinthisthang)
Traducteur: [CookingCryptos](https://twitter.com/CookingCryptos)